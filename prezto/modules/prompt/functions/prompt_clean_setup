function prompt_clean_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_clean_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and ruby-info before each command.
  add-zsh-hook precmd prompt_clean_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%B%F{green}%b%f%%b'
  zstyle ':prezto:module:git:info:clean' format ''
  zstyle ':prezto:module:git:info:dirty' format '%F{red}âœ—%f'
  zstyle ':prezto:module:git:info:commit' format '%F{blue}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{blue}%p%f'
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%D)'

  # Define prompts.
  local _host='%F{white}%B%m%b%f'
  local _dir='%B%c/%b'
  local _git='${git_info:+${(e)git_info[prompt]}}'
  PROMPT="${_host}:${_dir} ${_git} %(!.#.$) "
  RPROMPT='[%*]'
}

prompt_clean_setup "$@"
